#Область ОписаниеПеременных

Перем ИмяФайлаЗамеров;

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура РазобратьЗамеры(Конвертер) Экспорт
	
	ЗначенияЗамеров = ЗначенияЗамеров();
	Для каждого ДанныеПокрытияМодуля Из ЗначенияЗамеров.Data Цикл

		ModuleId = ДанныеПокрытияМодуля.ModuleId;

		СтрокаЗамера = Конвертер.НоваяСтрокаЗамера();
		ЗаполнитьЗначенияСвойств(СтрокаЗамера, ModuleId);

		Для Каждого LineInfo Из ДанныеПокрытияМодуля.LineInfo Цикл

			СтрокаЗамера.LineNo = LineInfo.LineNo;
			Конвертер.РазобратьДанныеЗамера(СтрокаЗамера);

		КонецЦикла;

	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриСозданииОбъекта(ПараметрИмяФайлаЗамеров)
	ИмяФайлаЗамеров = ПараметрИмяФайлаЗамеров;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЗначенияЗамеров()

	ЧтениеJSON = Новый ЧтениеJSON();
	ЧтениеJSON.ОткрытьФайл(ИмяФайлаЗамеров, КодировкаТекста.UTF8);
	Попытка
		ЗначенияЗамеров = ПрочитатьJSON(ЧтениеJSON);
		ЧтениеJSON.Закрыть();
	Исключение
		ЧтениеJSON.Закрыть();
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат ЗначенияЗамеров;

КонецФункции

#КонецОбласти
